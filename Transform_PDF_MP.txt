let
Transform_PDF = (TablePDF as binary ) as table =>
    let
        Origen = Pdf.Tables(TablePDF, [Implementation="1.3"]),
        SeleccionarTablas = Table.SelectRows(Origen, each ([Kind] = "Table")),
        Transformar = Table.TransformColumns( SeleccionarTablas,{{"Data", each 
            let
            PromoverEncabezados = Table.PromoteHeaders(_),
            LimpiarFilasNull = Table.SelectRows(PromoverEncabezados, each not List.IsEmpty(List.RemoveMatchingItems(Record.FieldValues(_), {"", null})) ),
            RenombrarCol = Table.TransformColumnNames(LimpiarFilasNull, each if Text.Contains( Text.Trim(_), "ID de la") then "ID de la operación" else _),
            LimpiarFilas = Table.SelectRows(RenombrarCol, each ([#"ID de la operación"] <> "operación")),
            ReemplazarNull = Table.ReplaceValue(LimpiarFilas, null, " ", Replacer.ReplaceValue, {"Descripción"})
            in
            ReemplazarNull
            }}),
        #"Texto insertado entre delimitadores" = Table.AddColumn(Transformar, "Page", each Text.BetweenDelimiters([Name], "(", ")"), type text),
        #"Otras columnas quitadas" = Table.SelectColumns(#"Texto insertado entre delimitadores",{"Page", "Data"}),
        #"Se expandió Data" = Table.ExpandTableColumn(#"Otras columnas quitadas", "Data", Table.ColumnNames( Table.Combine(#"Otras columnas quitadas"[Data]))),
        #"Columnas combinadas" = Table.CombineColumns(#"Se expandió Data",{"Fecha", "Descripción", "ID de la operación"},Combiner.CombineTextByDelimiter("",                 QuoteStyle.None),"Combinada"),
        #"Dividir columna por posición" = Table.SplitColumn(#"Columnas combinadas", "Combinada", Splitter.SplitTextByPositions({0, 10}, false), {"Fecha",                 "Combinada.2"}),
        ExtraerComprobante = Table.AddColumn(#"Dividir columna por posición", "ID de la operación", each 
        let 
            FuncionSeparar = Splitter.SplitTextByCharacterTransition({"0".."9"}, (c) => not List.Contains({"0".."9"},  c)),
            Separar =  FuncionSeparar (Text.Reverse( [Combinada.2])),
            Resultado = Text.Trim( Text.Reverse( List.First( Separar)))
        in
            Resultado
    ),
        ExtraerNombre = Table.AddColumn(ExtraerComprobante, "Descripción", each Text.Trim( Text.Clean( Text.Replace( Text.Start([Combinada.2],                             Text.Length([Combinada.2])- Text.Length([#"ID de la operación"])), "#(lf)", " "))), type text),
        #"Columnas reordenadas" = Table.ReorderColumns(ExtraerNombre,{"Page", "Descripción", "ID de la operación", "Valor", "Saldo"}),
        QuitarFilasVacias = Table.SelectRows(#"Columnas reordenadas", each [Valor] <> null and [Valor] <> "" and [Saldo] <> null and [Saldo] <> ""),
        #"Columnas quitadas" = Table.RemoveColumns(QuitarFilasVacias,{"Combinada.2"}),
        Resultado = try #"Columnas quitadas" otherwise Table.FromList({}) 
    in
        Resultado
in
Transform_PDF
